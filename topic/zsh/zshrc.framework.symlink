#!/usr/bin/env zsh

# -----
# setopts
# -----

# enable extended globbing
setopt EXTENDED_GLOB
# print null on empty glob
setopt NULL_GLOB
# disable no match error messages when globbing
setopt +o NOMATCH

# -----
# vars
# -----

typeset -U libs
typeset -U zsh_files
typeset -i updated_at

zsh_files=($DOTFILES/topic/**/*.zsh)

# -----
# zinit
# -----

# zinit
# ZINIT[HOME_DIR] -> ~/.zinit/
# ZINIT[BIN_DIR] -> ~/.zinit/bin
# ZINIT[COMPLETIONS_DIR] -> ~/.zinit/completions
# ZINIT[PLUGINS_DIR] -> ~/.zinit/plugins
# ZINIT[SNIPPETS_DIR] -> ~/.zinit/snippets
# ZINIT[SERVICES_DIR] -> ~/.zinit/services
source "${HOME}/.zinit/bin/zinit.zsh"

# -----
# framework
# -----

# load plugins
for plugins in ${(M)zsh_files:#*/plugins.zsh}; do
  source $plugins
done

# load aliases
for alias in ${(M)zsh_files:#*/aliases.zsh}; do
  source $alias
done

# load configs
for configs in ${(M)zsh_files:#*/configs.zsh}; do
  source $configs
done

# load paths
for paths in ${(M)zsh_files:#*/paths.zsh}; do
  source $paths
done

# compinit hack to make it faster
function zcompile-many() {
  local f
  for f; do zcompile -R -- "$f".zwc "$f"; done
}

# Enable the "new" completion system (compsys).
autoload -Uz compinit
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
  compinit
  [[ ~/.zcompdump.zwc -nt ~/.zcompdump ]] || zcompile-many ~/.zcompdump
  unfunction zcompile-many
else
  compinit -C
fi

ZSH_AUTOSUGGEST_MANUAL_REBIND=1

# load completions
for completion in ${(M)zsh_files:#*/completion.zsh}; do
  source $completion
done

# load key-bindings
for keys in ${(M)zsh_files:#*/keys.zsh}; do
  source $keys
done

for theme in ${(M)zsh_files:#*/zsh/theme.zsh}; do
  source $theme
done